#!pecmd

ENVI^ EnviMode=1
REGI HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\Nls\Language\Default,Lang
FIND $[ %Lang%<>0804 & %Lang%<>0404 ],
{
	TEAM ENVI Lang1=QemuBootTester|ENVI @WINmain=%Lang1%|ENVI Lang2=Hard Disk|ENVI Lang3=CD-ROM|ENVI Lang4=PXE|ENVI Lang5=Kernel|ENVI Lang6=Disk Image|ENVI Lang7=ISO|ENVI Lang8=FloppyImage|ENVI Lang9=FBA(1.6)|ENVI Lang10=Open|ENVI Lang11=server|ENVI Lang12=Boot file|ENVI Lang13=Select a Linux kernel|ENVI Lang14=Select a Linux kernel|ENVI Lang15=Select a Linux initial ram disk|ENVI Lang16=Select a Linux initial ram disk|ENVI Lang17=Cmdline|ENVI Lang18=Boot Mode|ENVI Lang19=Memory|ENVI Lang20=Std VGA
	TEAM ENVI Lang21=Run Qemu|ENVI Lang22=Free Memory|ENVI Lang23=CPU usage|ENVI Lang24=Select tftp server Folder|ENVI Lang25=Select a bootFILE from tftp server|ENVI Lang26=Select a|ENVI Lang27=Empty|ENVI Lang28=Select a FILE|ENVI Lang29=all|ENVI Lang30=Text file|ENVI Lang31=Enter cmdline|ENVI Lang32=or select a Textfile|ENVI Lang33=ReadOnly|ENVI Lang34=Command line text FILE|ENVI Lang35=Virtual Disk|ENVI Lang36=Set the path for the Imagefile|ENVI Lang37=Preparing ... Please Wait ... |ENVI Lang38=Boot Device|ENVI Lang39=Virtual CDR|ENVI Lang40=ADD Reg
}!   
{
	TEAM ENVI Lang1=Qemu启动测试器|ENVI @WINmain=%Lang1%|ENVI Lang2=磁盘测试|ENVI Lang3=光驱测试|ENVI Lang4=PXE 网启|ENVI Lang5=Linux内核|ENVI Lang6=磁盘镜像|ENVI Lang7=光盘镜像|ENVI Lang8=软盘镜像|ENVI Lang9=FBA(1.6)|ENVI Lang10=打 开|ENVI Lang11=服务器|ENVI Lang12=启动文件|ENVI Lang13=选择内核|ENVI Lang14=选择内核文件|ENVI Lang15=选择初始内存盘文件|ENVI Lang16=选择初始内存盘文件|ENVI Lang17=命令行|ENVI Lang18=启动模式|ENVI Lang19=内存|ENVI Lang20=标准VGA
	TEAM ENVI Lang21=开始测试|ENVI Lang22=可用内存|ENVI Lang23=CPU使用率|ENVI Lang24=请选择tftp目录|ENVI Lang25=从tftp服务器选择启动文件|ENVI Lang26=请选择一个|ENVI Lang27=无盘片|ENVI Lang28=浏览并选定一个文件|ENVI Lang29=全部格式|ENVI Lang30=文本文档|ENVI Lang31=输入命令行|ENVI Lang32=或选择一个命令行文本|ENVI Lang33=只读模式|ENVI Lang34=包含命令行的文本文件|ENVI Lang35=附加磁盘|ENVI Lang36=设置虚拟镜像路径|ENVI Lang37=正在准备文件...请稍候...|ENVI Lang38=启动设备|ENVI Lang39=附加光驱|ENVI Lang40=注册右键
}
FDIR --long --fullfile temp=%temp%
FDIR --long --fullfile desktop=%desktop%
ENVI AllFiles= .ISO  .DMG  .CDR  .IMG  .IMA  .GZ  .FBA  .PXE  .BIN  .0  .VHD  .VMDK  .MOS  .VDI  Drive
ENVI AllExt= ISO  DMG  CDR  IMG  IMA  GZ  FBA  PXE  BIN  0  VHD  VMDK  MOS  VDI
FIND $%*=/reg,
{
	FORX * %AllFiles%,tmpQTester,CALL RegAddQTest
	EXIT FILE
}
FIND $%*=/u,
{
	FORX * %AllFiles%,tmpQTester,CALL RegDelQTest
	EXIT FILE
}

//更好，不怕同名窗口
CHECK2 %*

_SUB CHECK2 *
	TEAM ENVI WID=0|ENVI RET=0
	LOCK --exist #QemuBootTester_527104427,RET
	IFEX $%QemuBootTester_527104427%>0,ENVI WID=%QemuBootTester_527104427%
	IFEX $[ 1=%RET% | %WID%>0 ],
	{
		IFEX $1=%RET%,!EXIT BLOCK
		CALC #WID = %WID%/1
		IFEX $%WID%>0,!
		{
			FIND --wid* WID,%Lang1%
			ENVI $QemuBootTester_527104427=%WID%
		}
		ENVI @@Visable=%WID%:2   //显示并激活窗口, 数值2则恢复窗口
		ENVI @@POS=%WID%:::::::1 //推到前面
		EXIT FILE
	}
	LOCK #QemuBootTester_527104427
	WINmain %*
	ENVI $QemuBootTester_527104427
_END

_SUB WINmain,W365H450,%Lang1%
	FIND $%DBG527104427_Test%=Test,!
	{
		ENVI $QemuBootTester_527104427=%&__WINID%
		TIME Timer4,1,CALL OnTimer4
	}

	IFEX %windir%\fonts\Tahoma*,ENVI @this.Font=10:Tahoma!ENVI @this.Font=9
	IFEX MEM=?,memory
	FIND |%memory%>4000,ENVI memory=1000!CALC #memory=%memory%/4
	SET$ &NL=0d 0a

	//开始、注册
	ITEM -font:18 -def go,L125T345W105H32,%Lang21%,CALL starting,
	CHEK regqtest,L20T340W65H15,%Lang40%,,0
	CHEK accel,L20T360W65H15,HAXM,,0
	CHEK stdvga,L265T340W70H15,%Lang20%,,1
	CHEK readonly,L265T360W80H15,%Lang33%,,1
	EDIT qemu,L0T380W359H20,,,0x8

	//隐藏，用于磁盘变化时，刷新磁盘
	EDIT disklist,L500T500W1H1,,,0x10
	EDIT cdromlist,L600T600W1H1,,,0x10

	//测试项目
	//GROU -center ,L10T5W340H55,
	RADI hdtest,L20T15W80H19,%Lang2%,CALL SetInit,1,1
	RADI cdtest,L100T15W80H19,%Lang3%,CALL SetInit,0,1
	RADI pxetest,L180T15W80H19,%Lang4%,CALL SetInit,0,1
	RADI linux,L260T15W80H19,%Lang5%,CALL SetInit,0,1
	RADI vhdtest,L20T35W80H19,%Lang6%,CALL SetInit,0,1
	RADI isotest,L100T35W80H19,%Lang7%,CALL SetInit,0,1
	RADI imgtest,L180T35W80H19,%Lang8%,CALL SetInit,0,1
	RADI fbatest,L260T35W80H19,%Lang9%,CALL SetInit,0,1
	RADI wimtest,L20T55W80H19,WIM,CALL SetInit,0,1
	RADI memdisk,L100T55W80H19,memdisk,CALL SetInit,0,1
	RADI fba7test,L180T55W80H19,FBA(1.7),CALL SetInit,0,1

	//启动设备
	LABE tmplabe,L20T82W80H19,%Lang38% : ,
	EDIT bootdevice,L100T79W235H20,,,0x8
	ITEM itembrow,L20T105W45H23,%Lang10%,CALL browtestfile,,16
	EDIT testfile,L70T107W270H20,,,0x110
	LIST phylist,L20T107W320H20
	//GROU -center tmpgrou,L10T60W340H75,

	//PXE网启
	ITEM browtftp,L20T75W70H23,tftp %Lang11%,CALL settftp,,16
	EDIT tftp,L95T77W245H20,,,0x10
	ITEM browbootfile,L20T102W70H23,%Lang12%,CALL selbootfile,,-16
	EDIT bootfile,L95T104W245H20,,,0x10

	//Linux内核
	ITEM browkernel,L20T75W50H23,Kernel,CALL browlinuxkernel,,16
	EDIT kernel,L75T77W265H20,%Lang14%,,0x110
	ITEM browinitrd,L20T105W50H23,initrd,CALL browlinuxinitrd,,16
	EDIT initrd,L75T107W265H20,%Lang16%,,0x110
	ITEM append,L20T135W50H23,%Lang17%,CALL browtext,,16
	LIST -h cmdline,L75T137W265H160,,,,0x114

	//附加磁盘
	//GROU -center ,L10T135W340H70,
	LABE ,L20T163W74H19,%Lang35% : ,
	EDIT vdisk,L105T165W235H20,,,0x8
	CHEK setvdiskfix,L20T189W14H20,,CALL creatdisk vdiskfix,0
	LIST vdiskfix,L34T189W147H20,,CALL creatdisk vdiskfix,
	CHEK setvdisksize,L194T189W14H20,,CALL creatdisk vdisksize,0
	LIST vdisksize,L208T189W60H20,64|128|256|512|1024|2048|3072|4096|5120|6144|7168|8192|10240|15360|20480|25600,CALL creatdisk vdisksize,1024,0x4
	CHEK setvmdk,L282T189W14H20,,CALL creatdisk vmdk,0
	ITEM vmdk,L295T189W45H23,...,CALL creatdisk vmdk,

	//附加光驱
	//GROU -center ,L10T205W340H70,
	LABE ,L20T223W76H19,%Lang39% : ,
	EDIT cdrom,L105T220W235H20,,,0x8
	CHEK setcdromfix,L20T246W14H20,,CALL creatdisk cdromfix,0
	LIST cdromfix,L34T246W235H20,,CALL creatdisk cdromfix,
	CHEK setiso,L282T246W14H20,,CALL creatdisk iso,0
	ITEM iso,L295T246W45H23,...,CALL creatdisk iso,

	//启动模式
	//GROU -center ,L10T275W340H33,
	LABE ,L20T287W80H19,%Lang18% : ,
	RADI efi64test,L105T284W75H19,EFI-x64,ENVI @stdvga.Enable=0,0,2
	RADI efi32test,L185T284W75H19,EFI-IA32,ENVI @stdvga.Enable=0,0,2
	RADI biostest,L265T284W80H19,Legacy BIOS,ENVI @stdvga.Enable=1,1,2

	//内存、CPU
	//GROU -center ,L10T308W160H33,
	LABE setmem,L20T318W40H15,%Lang19% :,
	LIST memory,L65T314W55H20,128|256|512|768|1024|2048|3072|4096|5120|6144|7168|8192,%memory%,1024,0x4
	LABE mem,L122T318W40H15,MB,
	LABE setcpu,L180T318W40H15,CPU :,
	LIST cpu,L225T314W55H20,1|2|3|4|5|6|7|8,%cpu%,1,0x4
	//GROU -center ,L175T308W175H33,
	

	//状态
	LABE+ ,L-1T400W390H1
	LABE -left label1,L5T404W155H20,,,0x444444
	LABE -left label2,L165T404W130H20,,,0x444444

	//开始按钮鼠标光标号
	ENVI @go.Cursor=32649

	//CPU使用率
	ENVI CPUS=1
	FIND --pid TMS,
	MSTR I1=<1>%TMS%
	MSTR T1=<2>%TMS%
	MSTR CPUS=<3>%TMS%
	SET Last=-100

	//准备工作
	CALL SetInit
	CALL creatdisk exit
	CALL OnTimer1
	CALL OnTimer2
	TIME Timer1,1000,CALL OnTimer1
	TIME Timer2,1,CALL OnTimer2
	TIME Timer3,1,CALL RunOnce %*

	//Linux内核命令行界面鼠标靠近提示
	ENVI @append.MSG=0x02A1: TEAM ENVI @@Cur=?heng;zong|TIPS* %Lang31%,%Lang32%,,,@aL%heng%T%zong%
	ENVI @append.MSG=0x02A3: TIPS *
_END

_SUB OnTimer4
	ENVI Tmp_527104427=
	REGI $HKLM\SYSTEM\CurrentControlSet\Control\Session Manager\Environment\QemuBootTester_527104427,Tmp_527104427
	IFEX $%Tmp_527104427%<>%&__WINID%,ENVI $QemuBootTester_527104427=%&__WINID%
_END

_SUB OnTimer1
	//实时刷新注册信息
	CALL RefreshReg
	//实时刷新状态栏
	FIND --pid TMS,
	MSTR I2=<1>%TMS%
	MSTR T2=<2>%TMS%
	CALC #I21=%I2% - %I1%
	CALC #T21=%T2% - %T1%
	CALC P= 100 - 100 * %I21% / %T21% / %CPUS% ##1
	FIND $%P%=-0.0, SET P=0
	IFEX $%P%<0, EXIT
	IFEX $%Last%=%P%, EXIT 
	SET Last=%P%
	ENVI I1=%I2%
	ENVI T1=%T2%
	IFEX MEM=?,Available_memory
	ENVI @label1=%Lang22%: %Available_memory% MB
	ENVI @label2=%Lang23%: %P%%%
_END

_SUB browtestfile
	FIND $%fbatest.Check%=1,BROW -fix imagefile,,%Lang26% %Lang9%,%Lang9% (*.fba)|*.FBA|%Lang29% ( * )|*.*|,0x1000!FIND $%fba7test.Check%=1,BROW -fix imagefile,,%Lang26% %Lang9%,%Lang9% (*.fba)|*.FBA,0x1000!FIND $%memdisk.Check%=1,BROW -fix imagefile,,%Lang26% %Lang8%,%Lang29% ( * )|*.*|,0x1000!FIND $%wimtest.Check%=1,BROW -fix imagefile,,%Lang26% WIM,WIM (*.wim)|*.wim|,0x1000!FIND $%isotest.Check%=1,BROW -fix imagefile,,%Lang26% %Lang7%,%Lang7% (*.iso;*.cdr;*.dmg)|*.iso;*.cdr;*.dmg|%Lang29% ( * )|*.*|,0x1000!FIND $%vhdtest.Check%=1,BROW -fix imagefile,,%Lang26% %Lang6%,%Lang6% (*.vhd;*.mos;*.vmdk;*.vdi;*.img;*.ima;*.raw)|*.ima;*.img;*.vhd;*.vmdk;*.vdi;*.mos;*.raw|%Lang29% ( * )|*.*|,0x1000!BROW -fix imagefile,,%Lang26% %Lang8%,%Lang8% (*.ima;*.img)|*.ima;*.img|%Lang29% ( * )|*.*|,0x1000
	FIND $%imagefile%=,!TEAM ENVI @testfile=%imagefile%|ENVI @bootdevice=%imagefile%
_END

_SUB settftp
	BROW -fix dir,*%desktop%,%Lang24%
	FIND $%dir%=,!ENVI @tftp=%dir%
_END

_SUB selbootfile
	CALL MakeLogic %tftp%
	BROW -fix tftpfile,%dir%\*,%Lang25%,all|*|,0x1000
	FIND $%tftpfile%=,EXIT _SUB
	FNAM -ext file_ext=%tftpfile%
	FDIR --full file_dir=%tftpfile%
	FIND $%file_dir%=%dir%,ENVI @bootfile=%file_ext%!CALL selbootfile
_END

//使路径合法化，并去掉最后一个\
_SUB MakeLogic
	FDIR --fullfile dir=%*
	FIND $%dir%=,EXIT _SUB
	MSTR tmpfile=-1,1,%dir%
	FIND $\=%tmpfile%,MSTR dir=-2,0,%dir%
_END

_SUB browlinuxkernel
	BROW -fix linuxfile,,%Lang13%,%Lang29% ( * )|*.*|,0x1000
	FIND $%linuxfile%=,!ENVI @kernel=%linuxfile%
_END

_SUB browlinuxinitrd
	BROW -fix linuxfile,,%Lang15%,%Lang29% ( * )|*.*|,0x1000
	FIND $%linuxfile%=,!ENVI @initrd=%linuxfile%
_END

_SUB browtext
	BROW -fix textfile,,%Lang26% %Lang34%,%Lang30% (*.txt;*.ini)|*.txt;*.ini|,0x1000
	FIND $%textfile%=,!
	{
		READ "%textfile%",**,&Str
		FIND $%&Str%=,!
		{
			ENVI @cmdline.VAL=
			ENVI @cmdline.ADD=%&Str%
			//设置默认选中项为第一行
			ENVI @cmdline.QUERY=1;FirstCmd
			ENVI @cmdline=:%FirstCmd%
		}
	}
_END

_SUB creatdisk
	FIND $%setvdiskfix.Check%=1,
	{
		FIND $vdiskfix=%1,!FIND $%1=exit,!EXIT BLOCK
		ENVI @setvdisksize.Check=0
		ENVI @setvmdk.Check=0
		ENVI @readonly.Check=1
		ENVI @vdiskfix.Enable=1
		ENVI @vdisksize.Enable=0
		ENVI @vmdk.Enable=0
		ENVI @readonly.Enable=0
		ENVI @vdisk=%vdiskfix%
	}
	FIND $%setvdisksize.Check%=1,
	{
		FIND $vdisksize=%1,!FIND $%1=exit,!EXIT BLOCK
		ENVI @setvdiskfix.Check=0
		ENVI @setvmdk.Check=0
		ENVI @vdiskfix.Enable=0
		ENVI @vdisksize.Enable=1
		ENVI @vmdk.Enable=0
		ENVI @readonly.Enable=1
		ENVI @vdisk=
		CALC #TmpSize = %vdisksize%/1
		FIND $%vdisksize% = %TmpSize%,!EXIT BLOCK
		IFEX $%TmpSize% <= 0,EXIT BLOCK
		IFEX $%TmpSize% > 999999999,EXIT BLOCK
		ENVI @vdisk=%vdisksize%M
	}
	FIND $%setvmdk.Check%=1,
	{
		FIND $vmdk=%1,!FIND $%1=exit,!EXIT BLOCK
		ENVI @setvdiskfix.Check=0
		ENVI @setvdisksize.Check=0
		ENVI @vdiskfix.Enable=0
		ENVI @vdisksize.Enable=0
		ENVI @vmdk.Enable=1
		ENVI @readonly.Enable=1
		BROW -fix vd,,%Lang36%,(*.vhd;*.mos;*.vmdk;*.vdi;*.img;*.ima)|*.ima;*.img;*.vhd;*.vmdk;*.vdi;*.mos|%Lang29% ( * )|*.*|,0x1000
		ENVI @vdisk=%vd%
	}
	FIND $%setcdromfix.Check%=1,
	{
		FIND $cdromfix=%1,!FIND $%1=exit,!EXIT BLOCK
		ENVI @setiso.Check=0
		ENVI @cdromfix.Enable=1
		ENVI @iso.Enable=0
		//MSTR tmpvol=3,2,%cdromfix%
		//FDRV *vol tmpvolume=%tmpvol%
		//FIND $%tmpvolume%=,!ENVI @cdrom=%cdromfix%
		MSTR tmpvol=3,1,%cdromfix%
		IFEX %tmpvol%:\,ENVI @cdrom=%cdromfix%!ENVI @cdrom=
	}
	FIND $%setiso.Check%=1,
	{
		FIND $iso=%1,!FIND $%1=exit,!EXIT BLOCK
		ENVI @setcdromfix.Check=0
		ENVI @cdromfix.Enable=0
		ENVI @iso.Enable=1
		BROW -fix cdh,,%Lang36%,(*.iso;*.cdr;*.dmg)|*.iso;*.cdr;*.dmg|%Lang29% ( * )|*.*|,0x1000
		ENVI @cdrom=%cdh%
	}
_END

_SUB RunOnce
	//关闭定时器
	ENVI @Timer3=0
	//处理右键传递过来的文件，直接开始测试
	FIND $%*=,EXIT _SUB
	CALL MakeLogic %*
	ENVI file=%dir%
	FEXT extname=%file%
	//不带扩展名，当作驱动器处理
	FIND $%extname%=,
	{
		FDRV tmpdriver=*:
		FORX * %tmpdriver%,tmpdrv,
		{
			FIND $%tmpdrv%=%file%,EXIT FORX!ENVI tmpdrv=
		}
		FIND $%tmpdrv%=,EXIT _SUB
		CALL RunOnce_Drv
		EXIT _SUB
	}
	//处理文件
	IFEX %file%,!EXIT _SUB
	FORX * %AllExt%,tmpext,FIND $%extname%=%tmpext%,TEAM ENVI tmpext=|EXIT FORX
	FIND $%tmpext%=,!EXIT _SUB
	//重新设置测试项
	CALL RunOnce_Test
	FIND $%extname%=ISO,ENVI @isotest.Check=1
	FIND $%extname%=DMG,ENVI @isotest.Check=1
	FIND $%extname%=CDR,ENVI @isotest.Check=1
	FIND $%extname%=IMG,ENVI @memdisk.Check=1
	FIND $%extname%=IMA,ENVI @memdisk.Check=1
	FIND $%extname%=GZ,ENVI @memdisk.Check=1
	FIND $%extname%=FBA,ENVI @fbatest.Check=1
	FIND $%extname%=PXE,ENVI @pxetest.Check=1
	FIND $%extname%=BIN,ENVI @pxetest.Check=1
	FIND $%extname%=0,ENVI @pxetest.Check=1
	FIND $%extname%=VHD,ENVI @vhdtest.Check=1
	FIND $%extname%=VMDK,ENVI @vhdtest.Check=1
	FIND $%extname%=MOS,ENVI @vhdtest.Check=1
	FIND $%extname%=VDI,ENVI @vhdtest.Check=1
	//重新设置启动设备
	CALL SetInit
	FDIR --full dir=%file%
	ENVI @tftp=%dir%
	TEAM FNAM -ext bootfile=%file%
	ENVI @bootfile=%bootfile%
	ENVI @bootdevice=%file%
	ENVI @testfile=%file%
	CALL starting
_END

_SUB RunOnce_Drv
	//重新设置测试项
	CALL RunOnce_Test
	ENVI @hdtest.Check=1
	FORM DrvType=%file%
	FIND $%DrvType%=DRIVE_CDROM,
	{
		IFEX %file%\,!EXIT _SUB
		ENVI @cdtest.Check=1
		ENVI @hdtest.Check=0
	}
	FIND $%DrvType%=DRIVE_CDROMUSB,
	{
		IFEX %file%\,!EXIT _SUB
		ENVI @cdtest.Check=1
		ENVI @hdtest.Check=0
	}
	CALL SetInit
	//找出盘符所对应的分区信息，重新设置启动设备
	ENVI AllDrv=%diskinfo%|%cdroms%
	FDRV FileDrv=%file%
	SED &AllDrv=0,|,%&NL%,%AllDrv%
	FORX *NL &AllDrv,OneDrv,
	{
		ENVI @bootdevice=
		FORX * %OneDrv%,ChoDrv,
		{
			FIND $%ChoDrv%=%FileDrv%,TEAM ENVI @bootdevice=%OneDrv%|ENVI @phylist=%OneDrv%|EXIT FORX
		}
		FIND $%bootdevice%=,!EXIT FORX
	}
	FIND $%bootdevice%=,EXIT _SUB
	CALL starting
_END

_SUB RunOnce_Test
	ENVI @hdtest.Check=0
	ENVI @cdtest.Check=0
	ENVI @pxetest.Check=0
	ENVI @linux.Check=0
	ENVI @vhdtest.Check=0
	ENVI @isotest.Check=0
	ENVI @imgtest.Check=0
	ENVI @fbatest.Check=0
	ENVI @fba7test.Check=0
	ENVI @wimtest.Check=0
	ENVI @memdisk.Check=0
_END

_SUB SetInit
	FIND $%hdtest.Check%=1,TEAM CALL efistatus 1|CALL Ondisktest
	FIND $%cdtest.Check%=1,TEAM CALL efistatus 1|CALL Oncdromtest
	FIND $%pxetest.Check%=1,TEAM CALL efistatus 1|CALL OnPXEtest
	FIND $%linux.Check%=1,TEAM CALL efistatus 1|CALL Linuxtest
	FIND $%vhdtest.Check%=1,TEAM CALL efistatus 1|CALL filetest
	FIND $%isotest.Check%=1,TEAM CALL efistatus 1|CALL filetest
	FIND $%imgtest.Check%=1,TEAM CALL efistatus 0|CALL filetest
	FIND $%fbatest.Check%=1,TEAM CALL efistatus 0|CALL filetest
	FIND $%fba7test.Check%=1,TEAM CALL efistatus 0|CALL filetest
	FIND $%wimtest.Check%=1,TEAM CALL efistatus 0|CALL filetest
	FIND $%memdisk.Check%=1,TEAM CALL efistatus 0|CALL filetest
_END

_SUB efistatus
	ENVI @efi64test.Enable=%1
	ENVI @efi32test.Enable=%1
	FIND $%1=0,TEAM ENVI @efi32test.Check=0|ENVI @efi64test.Check=0|ENVI @biostest.Check=1|ENVI @stdvga.Enable=1
_END

_SUB Ondisktest
	ENVI @browtftp.visable=0
	ENVI @tftp.visable=0
	ENVI @browbootfile.visable=0
	ENVI @bootfile.visable=0
	ENVI @itembrow.visable=0
	ENVI @testfile.visable=0
	ENVI @browkernel.visable=0
	ENVI @kernel.visable=0
	ENVI @browinitrd.visable=0
	ENVI @initrd.visable=0
	ENVI @append.visable=0
	ENVI @cmdline.visable=0
	`ENVI @phylist.POS=15:70:::
	ENVI @phylist.visable=1
	ENVI @tmpgrou.visable=1
	ENVI @tmplabe.visable=1
	ENVI @bootdevice.visable=1
	CALL listalldisk -all
	ENVI @phylist.VAL=%diskinfo%
	ENVI @vdiskfix.VAL=%diskinfo%
	ENVI @phylist=:%FirstHDInfo%
	ENVI @vdiskfix=:%FirstHDInfo%
_END

_SUB Oncdromtest
	ENVI @browtftp.visable=0
	ENVI @tftp.visable=0
	ENVI @browbootfile.visable=0
	ENVI @bootfile.visable=0
	ENVI @itembrow.visable=0
	ENVI @testfile.visable=0
	ENVI @browkernel.visable=0
	ENVI @kernel.visable=0
	ENVI @browinitrd.visable=0
	ENVI @initrd.visable=0
	ENVI @append.visable=0
	ENVI @cmdline.visable=0
	`ENVI @phylist.POS=15:70:::
	ENVI @phylist.visable=1
	ENVI @tmpgrou.visable=1
	ENVI @tmplabe.visable=1
	ENVI @bootdevice.visable=1
	CALL listcdrom
	ENVI @phylist.VAL=%cdroms%
	ENVI @cdromfix.VAL=%cdroms%
	ENVI @phylist=:%FirstCDInfo%
	ENVI @cdromfix=:%FirstCDInfo%
_END

_SUB OnPXEtest
	ENVI @browkernel.visable=0
	ENVI @kernel.visable=0
	ENVI @browinitrd.visable=0
	ENVI @initrd.visable=0
	ENVI @phylist.visable=0
	ENVI @tmpgrou.visable=1
	ENVI @tmplabe.visable=0
	ENVI @bootdevice.visable=0
	ENVI @append.visable=0
	ENVI @cmdline.visable=0
	`ENVI @phylist.POS=500:500:::
	ENVI @itembrow.visable=0
	ENVI @testfile.visable=0
	ENVI @browtftp.visable=1
	ENVI @tftp.visable=1
	ENVI @browbootfile.visable=1
	ENVI @bootfile.visable=1
	ENVI @tftp=
	ENVI @bootfile=
_END

_SUB Linuxtest
	ENVI @browtftp.visable=0
	ENVI @tftp.visable=0
	ENVI @browbootfile.visable=0
	ENVI @bootfile.visable=0
	ENVI @itembrow.visable=0
	ENVI @testfile.visable=0
	ENVI @phylist.visable=0
	ENVI @tmpgrou.visable=0
	ENVI @tmplabe.visable=0
	ENVI @bootdevice.visable=0
	`ENVI @phylist.POS=500:500:::
	ENVI @browkernel.visable=1
	ENVI @kernel.visable=1
	ENVI @browinitrd.visable=1
	ENVI @initrd.visable=1
	ENVI @append.visable=1
	ENVI @cmdline.visable=1
	ENVI @kernel=%Lang14%
	ENVI @initrd=%Lang16%
	ENVI @cmdline.VAL=A|B|C|D
	ENVI @cmdline=
_END

_SUB filetest
	ENVI @bootdevice=
	ENVI @browtftp.visable=0
	ENVI @tftp.visable=0
	ENVI @browbootfile.visable=0
	ENVI @bootfile.visable=0
	ENVI @browkernel.visable=0
	ENVI @kernel.visable=0
	ENVI @browinitrd.visable=0
	ENVI @initrd.visable=0
	ENVI @phylist.visable=0
	ENVI @append.visable=0
	ENVI @cmdline.visable=0
	`ENVI @phylist.POS=500:500:::
	ENVI @tmpgrou.visable=1
	ENVI @tmplabe.visable=1
	ENVI @bootdevice.visable=1
	ENVI @itembrow.visable=1
	ENVI @testfile.visable=1
	ENVI @testfile=%Lang28%
_END

_SUB listalldisk
	ENVI diskinfo=
	FIND $%*=-all,ENVI listall=!ENVI listall=FIND $%dtype%=127,
	PART list disk,diskall
	FORX * %diskall%,disknum,
	{
		PART -devid list disk %disknum%,abc
		MSTR -delims:\ V12=<2>%abc%
		LPOS iPos=__,1,%V12%
		CALC #iPos=%iPos%-1
		LSTR V12=%iPos%,%V12%
		SED V12=0:0,PROD_, ,%V12%
		SED V12=0:0,VEN_,,%V12%
		SED V12=0:0,REV_, ,%V12%
		SED V12=0:0,_, ,%V12%
		SED V12=0:0,,,%V12%
		SED V12=1:0,DISK,,%V12%
		SED V12=0:0,&,,%V12%
		MSTR V2=<2>%abc%
		FIND #%V2%>=1073741824,TEAM CALC disksize=%V2%/1073741824 ##1|ENVI disksize=%disksize%GB!FIND #%V2%>=1048576,TEAM CALC disksize=%V2%/1048576 ##1|ENVI disksize=%disksize%MB!FIND #%V2%>=1024,TEAM CALC disksize=%V2%/1024 ##1|ENVI disksize=%disksize%KB!ENVI disksize=%disksize%B
		ENVI drives=
		FDRV AllDrive=*
		FORX * %AllDrive%,getdrive,
		{
			PART -fill list drv %getdrive%,drvinfo
			MSTR V9=<9>%drvinfo%
			FIND $%V9%=%disknum%,ENVI drives=%drives% %getdrive%:
		}
		FORM DriveType,Bustype=\\.\PhysicalDrive%disknum%
		ENVI dtype=%DriveType%%Bustype%
		FIND $%dtype%=117,ENVI diskinfo=%diskinfo%|RM%disknum%: %V12% ( %disksize%, %drives% )!%listall%ENVI diskinfo=%diskinfo%|HD%disknum%: %V12% ( %disksize%, %drives% )
	}
	SED &TmpDiskInfo=0,|,%&NL%,%diskinfo%
	FORX *NL &TmpDiskInfo,FirstHDInfo,FIND $%FirstHDInfo%=,!EXIT FORX
_END

_SUB listcdrom
	ENVI cdroms=
	FDRV allvolume=*:
	FORX * %allvolume%,volume,
	{
		FORM DriveType=%volume%
		IFEX %volume%,TEAM FDRV *vol V=%volume%|ENVI drvsta=%V%!ENVI drvsta=%Lang27%
		FIND $%DriveType%=DRIVE_CDROM,ENVI cdroms=%cdroms%|DVD/CD-ROM %volume% (  %drvsta%  ) !FIND $%DriveType%=DRIVE_CDROMUSB,ENVI cdroms=%cdroms%|USB CD-ROM %volume% (  %drvsta%  ) 
	}
	MSTR cdroms=2,0,%cdroms%
	SED &TmpCDInfo=0,|,%&NL%,%cdroms%
	FORX *NL &TmpCDInfo,FirstCDInfo,FIND $%FirstCDInfo%,!EXIT FORX
_END

_SUB starting
	ENVI @Timer1=0
	ENVI @go.Enable=0
	CALL Display 0
	ENVI @label1=%Lang37%
	ENVI @label2=
	FILE --force "%temp%\QemuBootTester"
	MDIR %temp%\QemuBootTester
	EXEC --hide --wait --m --exe:#5
	ENVI options=-L "%temp%\QemuBootTester" -m %memory% -smp %cpu% -localtime
	FIND $%biostest.Check%=1,
	{
		FIND $%stdvga.Check%=1,ENVI options=%options% -vga std
	}
	FIND $%efi64test.Check%=1,ENVI options=%options% -bios efi64.bin!FIND $%efi32test.Check%=1,ENVI options=%options% -bios efi32.bin
	FIND $%readonly.Check%=1,ENVI options=%options% -snapshot
	FIND $%accel.Check%=1,ENVI options=%options% -accel hax
	FIND $%hdtest.Check%=1,TEAM MSTR disknum=5,1,%phylist%|ENVI options=%options% -hda \\.\PhysicalDrive%disknum% -boot c
	FIND $%cdtest.Check%=1,TEAM MSTR cdvolume=3,1,%phylist%|ENVI options=%options% -cdrom \\.\%cdvolume%: -boot d
	FIND $%pxetest.Check%=1,ENVI options=%options% -boot n -net nic,model=e1000 -net user,tftp="%tftp%",bootfile="%bootfile%"
	FIND $%fbatest.Check%=1,
	{
		EXEC --hide --wait --m --exe:#4 =fba2img_exe "%testfile%" "%temp%\QemuBootTester\test.img"
		ENVI options=%options% -hda "%temp%\QemuBootTester\test.img" -boot c
	}
	FIND $%fba7test.Check%=1,ENVI options=%options% -kernel "%temp%\QemuBootTester\grub.exe" -initrd "%testfile%" -append "--config-file=map (rd)+1 (hd0);map --hook;chainloader (hd0)/grldr"
	FIND $%imgtest.Check%=1,ENVI options=%options% -fda "%testfile%" -boot a
	FIND $%wimtest.Check%=1,ENVI options=%options% -kernel "%temp%\QemuBootTester\grub.exe" -initrd "%testfile%" -append "--config-file=root (hd0);kernel /wimboot gui;initrd @bootmgr=/bootmgr @bcd=/bcd @boot.sdi=/boot.sdi @boot.wim=(rd)+1" -hda "%temp%\QemuBootTester\wimboot.fba"
	FIND $%memdisk.Check%=1,ENVI options=%options% -kernel "%temp%\QemuBootTester\memdisk" -initrd "%testfile%" -append "raw" -boot c
	FIND $%vhdtest.Check%=1,ENVI options=%options% -hda "%testfile%" -boot c
	FIND $%isotest.Check%=1,ENVI options=%options% -cdrom "%testfile%" -boot d
	FIND $%linux.Check%=1,
	{
		ENVI options=%options% -kernel "%kernel%"
		FIND $%initrd%=,!
		{
			FIND $%initrd%=%Lang16%,!ENVI options=%options% -initrd "%initrd%"
		}
		FIND $%cmdline%=,!ENVI options=%options% -append "%cmdline%"
		ENVI options=%options% -boot c
	}
	FIND $%vdisk%=,!
	{
		FIND $%setvdiskfix.Check%=1,TEAM MSTR vhdnum=5,1,%vdisk%|ENVI options=%options% -hdb \\.\PhysicalDrive%vhdnum%
		FIND $%setvdisksize.Check%=1,
		{
			EXEC =!"%temp%\QemuBootTester\qemu-img.exe" create -q -f vmdk "%temp%\QemuBootTester\hd.vmdk" %vdisk%
			ENVI options=%options% -hdb "%temp%\QemuBootTester\hd.vmdk"
		}
		FIND $%setvmdk.Check%=1,ENVI options=%options% -hdb "%vdisk%"
	}
	FIND $%cdrom%=,!
	{
		FIND $%setcdromfix.Check%=1,TEAM MSTR vcdvolume=3,1,%cdrom%|ENVI options=%options% -cdrom \\.\%vcdvolume%:
		FIND $%setiso.Check%=1,ENVI options=%options% -cdrom "%cdrom%"
	}
	ENVI @qemu="%temp%\QemuBootTester\qemu-system-x86_64w.exe" %options%
	ENVI @Timer1=1000
	EXEC =%qemu%
	FILE --force "%temp%\QemuBootTester"
	FILE --force "%Temp%\~pecmd*.tmp"
	FILE --force "%Temp%\~tmp*.tmp"
	FILE --force "%temp%\qem*.tmp"
	CALL Display 1
	ENVI @@Visable=%&__WINID%:2
	ENVI @@POS=%&__WINID%:::::::1
_END

_SUB Display
	ENVI @Timer2=%1
	ENVI @setvdiskfix.Enable=%1
	ENVI @vdiskfix.Enable=%1
	ENVI @setvdisksize.Enable=%1
	ENVI @vdisksize.Enable=%1
	ENVI @setvmdk.Enable=%1
	ENVI @vmdk.Enable=%1
	ENVI @setcdromfix.Enable=%1
	ENVI @cdromfix.Enable=%1
	ENVI @setiso.Enable=%1
	ENVI @iso.Enable=%1
	ENVI @hdtest.Enable=%1
	ENVI @cdtest.Enable=%1
	ENVI @pxetest.Enable=%1
	ENVI @linux.Enable=%1
	ENVI @vhdtest.Enable=%1
	ENVI @isotest.Enable=%1
	ENVI @imgtest.Enable=%1
	ENVI @fbatest.Enable=%1
	ENVI @fba7test.Enable=%1
	ENVI @wimtest.Enable=%1
	ENVI @memdisk.Enable=%1
	ENVI @phylist.Enable=%1
	ENVI @itembrow.Enable=%1
	ENVI @testfile.Enable=%1
	ENVI @browtftp.Enable=%1
	ENVI @tftp.Enable=%1
	ENVI @browbootfile.Enable=%1
	ENVI @bootfile.Enable=%1
	ENVI @browkernel.Enable=%1
	ENVI @kernel.Enable=%1
	ENVI @browinitrd.Enable=%1
	ENVI @initrd.Enable=%1
	ENVI @append.Enable=%1
	ENVI @cmdline.Enable=%1
	ENVI @efi64test.Enable=%1
	ENVI @efi32test.Enable=%1
	ENVI @biostest.Enable=%1
	ENVI @memory.Enable=%1
	ENVI @stdvga.Enable=%1
	ENVI @readonly.Enable=%1
	ENVI @regqtest.Enable=%1
_END

_SUB OnTimer2
	//实时刷新VGA状态
	FIND $%efi64test.Check%=1,ENVI @stdvga.Enable=0
	FIND $%efi32test.Check%=1,ENVI @stdvga.Enable=0
	FIND $%biostest.Check%=1,ENVI @stdvga.Enable=1
	//实时刷新附加磁盘、附加光驱
	CALL RefreshVirtual
	//实时刷新设备
	CALL Refreshtftp
	CALL Refreshdisk
	FIND $%phylist.visable%=1,
	{
		FIND $%bootdevice%=%phylist%,EXIT BLOCK
		FIND $%cdtest.Check%=1,
		{
			//MSTR tmpvol=3,2,%phylist%
			//FDRV *vol tmpvolume=%tmpvol%
			//FIND $%tmpvolume%=,ENVI @bootdevice=!ENVI @bootdevice=%phylist%
			MSTR tmpvol=3,1,%phylist%
			IFEX %tmpvol%:\,ENVI @bootdevice=%phylist%!ENVI @bootdevice=
		}!    
		{
			ENVI @bootdevice=%phylist%
		}
	}
	FIND $%testfile.visable%=1,
	{
		FIND $%testfile%=,EXIT BLOCK
		CALL MakeLogic %testfile%
		IFEX %dir%\,ENVI dir=
		IFEX %dir%,!ENVI dir=
		FIND $%bootdevice%=%dir%,EXIT BLOCK
		ENVI @bootdevice=%dir%
	}
	//实时刷新注册信息
	FIND $%regqtest.Check%=0,
	{
		FIND $%RegStatus%=1,
		{
			ENVI RegStatus=0
			FORX * %AllFiles%,tmpQTester,CALL RegDelQTest
		}
	}!   
	{
		FIND $%RegStatus%=0,
		{
			ENVI RegStatus=1
			FORX * %AllFiles%,tmpQTester,CALL RegAddQTest
		}
	}
	//实时刷新开始按钮
	FIND $%bootdevice.visable%=1,
	{
		FIND $%bootdevice%=,ENVI @go.Enable=0!ENVI @go.Enable=1
	}
	FIND $%pxetest.Check%=1,
	{
		FIND $%tftp%=,TEAM ENVI @go.Enable=0|EXIT BLOCK
		FIND $%bootfile%=,TEAM ENVI @go.Enable=0|EXIT BLOCK
		CALL MakeLogic %tftp%
		IFEX %dir%,!TEAM ENVI @go.Enable=0|EXIT BLOCK
		IFEX %dir%\,!TEAM ENVI @go.Enable=0|EXIT BLOCK
		IFEX %dir%\%bootfile%,!TEAM ENVI @go.Enable=0|EXIT BLOCK
		IFEX %dir%\%bootfile%\,TEAM ENVI @go.Enable=0|EXIT BLOCK
		FIND $%tftp%=%dir%,!ENVI @tftp=%dir%
		ENVI @go.Enable=1
	}
	FIND $%linux.Check%=1,
	{
		FIND $%kernel%=,TEAM ENVI @go.Enable=0|EXIT BLOCK
		CALL MakeLogic %kernel%
		IFEX %dir%,!TEAM ENVI @go.Enable=0|EXIT BLOCK
		IFEX %dir%\,TEAM ENVI @go.Enable=0|EXIT BLOCK
		FIND $%kernel%=%dir%,!ENVI @kernel=%dir%
		FIND $%initrd%=,TEAM ENVI @go.Enable=1|EXIT BLOCK
		CALL MakeLogic %initrd%
		IFEX %dir%\,TEAM ENVI @go.Enable=0|EXIT BLOCK
		IFEX %dir%,!FIND $%initrd%=%Lang16%,TEAM ENVI @go.Enable=1|EXIT BLOCK!TEAM ENVI @go.Enable=0|EXIT BLOCK
		FIND $%initrd%=%dir%,!ENVI @initrd=%dir%
		ENVI @go.Enable=1
	}
_END

_SUB RefreshVirtual
	FIND $%setvdiskfix.Check%=0,TEAM ENVI @readonly.Enable=1|ENVI @vdiskfix.Enable=0!TEAM ENVI @readonly.Check=1|ENVI @readonly.Enable=0
	FIND $%setvdisksize.Check%=0,ENVI @vdisksize.Enable=0
	FIND $%setvmdk.Check%=0,ENVI @vmdk.Enable=0
	FIND $%setcdromfix.Check%=0,ENVI @cdromfix.Enable=0
	FIND $%setiso.Check%=0,ENVI @iso.Enable=0
	FIND $%setvdiskfix.Check%=0,
	{
		FIND $%setvdisksize.Check%=0,
		{
			FIND $%setvmdk.Check%=0,
			{
				ENVI @vdisk=
				ENVI @vdiskfix.Enable=0
				ENVI @vdisksize.Enable=0
				ENVI @vmdk.Enable=0
			}
		}
	}
	FIND $%setcdromfix.Check%=0,
	{
		FIND $%setiso.Check%=0,
		{
			ENVI @cdrom=
			ENVI @cdromfix.Enable=0
			ENVI @iso.Enable=0
		}
	}
	FIND $%cdtest.Check%=1,CALL Refreshvcd!
	{
		FIND $%isotest.Check%=1,CALL Refreshvcd!TEAM ENVI @setcdromfix.Enable=1|ENVI @setiso.Enable=1
	}
_END

_SUB Refreshvcd
	ENVI @setcdromfix.Check=0
	ENVI @setcdromfix.Enable=0
	ENVI @setiso.Check=0
	ENVI @setiso.Enable=0
_END

_SUB Refreshtftp
	FIND $%tftp%=,TEAM ENVI @browbootfile.Enable=0|EXIT _SUB
	CALL MakeLogic %tftp%
	IFEX %dir%\,ENVI @browbootfile.Enable=1!TEAM ENVI @browbootfile.Enable=0|ENVI @bootfile=
_END

//磁盘分区变更，即刻刷新
_SUB Refreshdisk
	PART list disk,D
	ENVI D1=%disklist%
	FIND $%D%=%D1%,!
	{
		ENVI @disklist=%D%
		CALL listalldisk -all
		FIND $%hdtest.Check%=1,TEAM ENVI @phylist.VAL=%diskinfo%|ENVI @phylist=:%FirstHDInfo%
		ENVI @vdiskfix.VAL=%diskinfo%
		ENVI @vdiskfix=:%FirstHDInfo%
	}
	FDRV D=*:
	ENVI D1=%cdromlist%
	FIND $%D%=%D1%,!
	{
		ENVI @cdromlist=%D%
		CALL listcdrom
		FIND $%cdtest.Check%=1,TEAM ENVI @phylist.VAL=%cdroms%|ENVI @phylist=:%FirstCDInfo%
		ENVI @cdromfix.VAL=%cdroms%
		ENVI @cdromfix=:%FirstCDInfo%
	}
_END

_SUB RefreshReg
	ENVI TmpStatus=1
	FORX * %AllFiles%,tmpQTester,CALL DetectReg
	FIND $%TmpStatus%=1,TEAM ENVI RegStatus=1|ENVI @regqtest.Check=1!TEAM ENVI RegStatus=0|ENVI @regqtest.Check=0
_END

_SUB DetectReg
	ENVI QTester=
	REGI $HKCR\%tmpQTester%\,QTester
	FIND $%tmpQTester%=Drive,ENVI QTester=Drive
	FIND $%QTester%=,ENVI QTester=%tmpQTester%
	ENVI tmpQTester=
	REGI $HKCR\%QTester%\shell\QemuBootTester\command\,tmpQTester
	FIND $%tmpQTester%="%MyName%" "%%1",!ENVI TmpStatus=0
_END

_SUB RegDelQTest
	ENVI QTester=
	REGI $HKCR\%tmpQTester%\,QTester
	FIND $%tmpQTester%=Drive,ENVI QTester=Drive
	FIND $%QTester%=,ENVI QTester=%tmpQTester%
	REGI HKCR\%QTester%\shell\QemuBootTester\!
_END

_SUB RegAddQTest
	ENVI QTester=
	REGI $HKCR\%tmpQTester%\,QTester
	FIND $%tmpQTester%=Drive,ENVI QTester=Drive
	FIND $%QTester%=,ENVI QTester=%tmpQTester%
	REGI $HKCR\%QTester%\shell\QemuBootTester\=%Lang1%
	REGI $HKCR\%QTester%\shell\QemuBootTester\command\="%MyName%" "%%1"
	REGI $HKCR\%QTester%\shell\QemuBootTester\Icon="%MyName%",0
_END

